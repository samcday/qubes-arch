# Maintainer: Sam Day <me@samcday.com>
pkgname=qubes-db
pkgver=4.1.8
pkgrel=1
pkgdesc="Qubes component: core-qubesdb"
arch=(x86_64)
url="https://github.com/QubesOS/qubes-core-qubesdb"
license=(GPL)
depends=(libvchan-xen)
makedepends=(git python)
install=qubes-db.install

source=(
  "git+https://github.com/qubesos/qubes-core-qubesdb?tag=v${pkgver}"
  0001-daemon-systemd-new-style-daemon-changes.patch
  0002-daemon-systemd-socket-activation-support.patch
  0003-daemon-simplify-systemd-service-management.patch
  0004-daemon-systemd-service-hardening.patch
  )
sha512sums=('SKIP'
            'c069911f7b2cebda81e87dd212e4362015f19b3b9e0be03264594fdaac461ca4a3c342a6bf636f1c57d617a318397531a082d578f7c6e6220f42d575899e78e7'
            '96611bc643e36c76b3065d3309072d7946e7c5e1dc1a085b3e0db4913047dfa611e4d77649f4a4ad0209c1b99d94d756923add507b451a0f1c183605d47915cb'
            '02fe7f78b7b54570edfe466f381e43f4e7ddbef7f0e643a4813e9e2a260b50c9f50dc50f61be46f153c5e19154138727073377900cd4e3ab7c51ecfeffe10935'
            '1beeb1804cafe72ef084760388f4c0fe2b080f7d00cdcd492323df9c523486259ab9f8f7c622fce6319a38215ac60259a811cdb8d2622d9b76ba4eab32015096')

prepare() {
  {
    git -C ${srcdir}/qubes-core-qubesdb am ${srcdir}/*.patch
  }
}

build() {
  make -C ${srcdir}/qubes-core-qubesdb BACKEND_VMM=xen prefix=/usr all
}

package() {
  for dir in include python client daemon; do
    make -C ${srcdir}/qubes-core-qubesdb/${dir} DESTDIR="$pkgdir/" SBINDIR=/usr/bin LIBDIR=/usr/lib install
  done

  install -D -m 0644 "${srcdir}/qubes-core-qubesdb/daemon/qubes-db.service" "${pkgdir}/usr/lib/systemd/system/qubes-db.service"
  install -D -m 0644 "${srcdir}/qubes-core-qubesdb/daemon/qubes-db.socket" "${pkgdir}/usr/lib/systemd/system/qubes-db.socket"
}
