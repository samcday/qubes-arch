# Maintainer: Sam Day <me@samcday.com>
pkgname=qubes-db
pkgver=4.1.8
pkgrel=1
pkgdesc="Qubes component: core-qubesdb"
arch=(x86_64)
url="https://github.com/QubesOS/qubes-core-qubesdb"
license=(GPL)
depends=(libvchan-xen)
makedepends=(git python)
install=qubes-db.install

source=(
  "git+https://github.com/qubesos/qubes-core-qubesdb?tag=v${pkgver}"
  0001-Make-qubes-db-more-like-a-systemd-new-style-daemon.patch
  0002-add-support-for-systemd-socket-activation.patch
  0003-simplify-service-management.patch
  )
sha512sums=('SKIP'
            'f9176a2e68fb4383ef6545240de948b1b1ed3d6f8079e045b247853ac87e95a76efee8973d07dd410a27462e3cb197ecfb3144cb2caef8558b7342ad226103b3'
            'bdb792120e5605852dd7d89108b5a0b5a5d28af8b2800075ac2142ad03ef349c3522ba0340a0181804c382b262292839a32a6906f4a14533fb07abfe96e755dd'
            'ca0c57372842f052a6c35884d0acc970e183f6c36a15f9502816d28d01c21b8cd219845efa352c1864e8a15a1eab1db2c7ade1959fc3000c84f54bcb8fc77bb7')

prepare() {
  {
    git -C ${srcdir}/qubes-core-qubesdb am ${srcdir}/*.patch
  }
}

build() {
  make -C ${srcdir}/qubes-core-qubesdb BACKEND_VMM=xen prefix=/usr all
}

package() {
  for dir in include python client daemon; do
    make -C ${srcdir}/qubes-core-qubesdb/${dir} DESTDIR="$pkgdir/" SBINDIR=/usr/bin LIBDIR=/usr/lib install
  done

  install -D -m 0644 "${srcdir}/qubes-core-qubesdb/daemon/qubes-db.service" "${pkgdir}/usr/lib/systemd/system/qubes-db.service"
  install -D -m 0644 "${srcdir}/qubes-core-qubesdb/daemon/qubes-db.socket" "${pkgdir}/usr/lib/systemd/system/qubes-db.socket"
}
