# Maintainer: Sam Day <me@samcday.com>
pkgname=(qubes-qrexec qubes-qrexec-dom0 qubes-qrexec-vm)
pkgbase=qrexec
pkgver=4.1.10
pkgrel=1
pkgdesc="qrexec, the Qubes OS Remote Procedure Call (RPC) protocol."
arch=(x86_64)
url="https://github.com/QubesOS/qubes-core-qrexec"
license=('GPL')
depends=(libvchan-xen python python-gbulb)
makedepends=(
  git
  lsb-release
  pandoc
  python-asynctest
  python-coverage
  python-dbus
  python-psutil
  python-pyinotify
  python-pytest
  python-recommonmark
  python-sphinx
  python-systemd
  )
source=(
  "git+https://github.com/qubesos/qubes-core-qrexec?tag=v${pkgver}"
  0001-Change-install-targets-to-use-BINDIR-SBINDIR-LIBDIR.patch
  0002-qrexec-policy-daemon-systemd-improvements.patch
  0003-qrexec-daemon-socket-activation-support.patch
  0004-qrexec-daemon-systemd-sd_notify-support.patch
  0005-qrexec-daemon-systemd-journald-native-logging-suppor.patch
  0006-Fix-dep-targets-in-Makefile.patch
  )
sha512sums=('SKIP'
            '89ba42e4db5dade4de0a7e0d32c26ea051290bd6cf819f36de264d844055e7f40f6be4c0eb4501f9cfa6858191caf55407737d5a1bd485f760c07ba66920559c'
            'ace574bbe359355649328a8fd51c1f2644ace13014d042b623ca89b75b2694b6704419efdb00c77d0f8146660e26caaa794d80ead3e95185bec3a124ec8e3220'
            '9edcc24acd6c1bb50656c0d0a581600c2a08c06a623cc835d200faa93a1fd7fa47991b118218acf953e4f138ba61e5ebffe3823bbe30a2e22b926dfb24330739'
            '0070ae8cdfb4ca60b26d65b8472ff222e86bf190ada43182571ffcffff40867a0a0069fd100fdfd9b90722d528b58b9d7a969d70b1df8d292f55e9ed1dc17944'
            '8614fb1cbae7ee847007797e4c15b95dc0cdf1b40b0a4863c7482805e2604af8084db75a2837d0f1e24ccfd6e5e13e14c233c8f97fcc96ec2db108def35e018d'
            'f9bec65240363ba2564c3080853b4f5532def187b03083dc6b2d8d232994e2b14d74222fd311aa9a7dece70d4c39fcac514fa557a62c6dc9f8ad08ead1f72c69')

prepare() {
  git -C ${srcdir}/qubes-core-qrexec am ${srcdir}/*.patch
}

build() {
  BACKEND_VMM=xen make -C "${srcdir}/qubes-core-qrexec" prefix=/usr all
}

check() {
  # Disabled atm, qrexec/tests/socket/daemon.py::TestDaemon::test_client_exec hangs.
  # if pkg-config vchan-socket; then
  if false; then
    echo "libvchan-socket is available, including socket tests"
    cp -R "qubes-core-qrexec" "qubes-core-qrexec-socket"
    cd qubes-core-qrexec-socket

    export CFLAGS="--coverage -DCOVERAGE"
    export LDFLAGS=--coverage
    make -C libqrexec BACKEND_VMM=socket
    make -C agent BACKEND_VMM=socket
    make -C daemon BACKEND_VMM=socket
  else
    echo "libvchan-socket not available, skipping socket tests"
    cd qubes-core-qrexec
    export SKIP_SOCKET_TESTS=1
  fi

  python3 -m coverage run -m pytest qrexec/tests -o python_files=*.py -v "$@"
}

package_qubes-qrexec() {
  make -C "${srcdir}/qubes-core-qrexec" DESTDIR="$pkgdir/" install-base
}

package_qubes-qrexec-dom0() {
  depends+=(qrexec qubes-dom0)
  conflicts+=(qrexec-vm)
  install=qrexec-dom0.install

  make -C "${srcdir}/qubes-core-qrexec" DESTDIR="$pkgdir/" LIBDIR=/usr/lib SBINDIR=/usr/bin install-dom0
}

package_qubes-qrexec-vm() {
  depends+=(qrexec)
  conflicts+=(qrexec-dom0)

  make -C qubes-core-qrexec DESTDIR="$pkgdir/" install-vm
}
