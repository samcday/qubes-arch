# Maintainer: Sam Day <me@samcday.com>
pkgname=qubes-admin-client
pkgver=4.1.10
pkgrel=1
pkgdesc="Qubes management API client "
arch=(x86_64)
url=""
license=('LGPL2.1')
depends=(
  python
  python-daemon
  python-docutils
  python-lxml
  python-xcffib
  scrypt
  xorg-xrandr)
makedepends=(
  python-asynctest
  python-coverage
  python-dbus
  python-setuptools
  python-sphinx
  python-yaml
  )
optdepends=()
provides=()
conflicts=()
replaces=()
backup=()
options=()
install=
changelog=
source=(
  "git+https://github.com/QubesOS/qubes-core-admin-client?tag=${pkgver}"
  "0001-Remove-check-for-etc-qubes-release.patch"
  )
sha512sums=('SKIP'
            '876d73371277f63228f5d26ff002f2c039ddc9d624b2eb86c261f328748f48345d647b258aba04a1015349d382907248ddf7e862bc564d8c92da28baf858dcf3')

prepare() {
  git -C ${srcdir}/qubes-core-admin-client am ${srcdir}/*.patch
}

build() {
  cd qubes-core-admin-client
  make all
}

check() {
  cd qubes-core-admin-client

  : "${PYTHON:=python3}"
  : "${ROOTDIR:=.}"
  : "${TESTPYTHONPATH:=$ROOTDIR/test-packages}"

  PYTHONPATH="${TESTPYTHONPATH}:${PYTHONPATH}"
  export PYTHONPATH

  [ -r version ] || ln -s ${ROOTDIR}/version ./
  [ -r setup.py ] || ln -s ${ROOTDIR}/setup.py ./
  "${PYTHON}" ./setup.py egg_info --egg-base "${TESTPYTHONPATH}"
  "${PYTHON}" -m coverage run --rcfile=$ROOTDIR/ci/coveragerc -m unittest discover -s qubesadmin/tests -p '*.py' -v "$@"

}

package() {
  make -C "${srcdir}/qubes-core-admin-client" DESTDIR=${pkgdir} install

  # Remove XDG autostart stuff, we're using systemd.
  rm ${pkgdir}/etc/xdg/autostart/qvm-start-daemon.desktop
  rm ${pkgdir}/etc/xdg/autostart/qvm-start-daemon-kde.desktop

  # TODO, probably delete this also?
  # /etc/qubes/post-install.d/30-keyboard-layout-service.sh

  find ${pkgdir} -type d -empty -delete
}
