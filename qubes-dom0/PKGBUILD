# Maintainer: Sam Day <me@samcday.com>
pkgname=qubes-dom0
pkgver=mm_67faa7c1
pkgrel=1
pkgdesc="Qubes component: core-admin"
arch=(x86_64)
url="https://github.com/qubesos/qubes-core-admin"
license=('GPL')
depends=(
  python
  python-dbus
  python-docutils
  python-jinja
  python-lxml
  python-setuptools
  python-systemd
  python-pyaml
  xen-qubes
  libvirt-python
  pciutils
  qubes-db
  )
makedepends=(
  imagemagick
  lvm2
  python-dbus
  python-sphinx
  )
install=qubes-dom0.install
source=(
  "git+https://github.com/qubesos/qubes-core-admin?tag=${pkgver}"
  qubes.sysusers
  0001-make-runtime-dir-in-tmpfiles-qubes.conf-configurable.patch
  0002-qmemman-systemd-improvements.patch
  0003-qubesd-systemd-improvements.patch
  0004-qubes-special-case-dom0-qubesdb-connection.patch
  0005-qubesd-systemd-socket-activation-support.patch
  0006-qubesd-use-systemd-to-manage-per-qube-daemons.patch
  )
sha512sums=('SKIP'
            '7dcff76193b9b802e896e3ce2ae87eff0b6b78808eb730144625ed12d3a0e1ce29585a86a2a23bbceeff586dfaf621c9a9e127935eabcc9a7f022da125927065'
            '04f1675c33a1715f945c5de60440fdaeff0e97f87ca18e90a19cce0b4dfb06234e636d4f3f0c5d80369494eb716a89dd25ebeb22902f84e9cbf48e51b84b731f'
            'e741dc0d76693b6b54b3616312d9b434c998a7b22dc94bf3b4c1236271007c9c37f3b3c95029fcc5e75d79a4b9e98a6b22693ae07466959f5fc866567889ffe1'
            'ef34245ff878f169fcd3fe47c2e76224beb2b5402282c1988a53ac0c9b72a5d94712dfbee0c204fbbee6c007bcebc2df0ecfa11fd29dc1f963d064258b9d2291'
            '5c40bcca945bd12f27e78c7c6d1df753f038a85f688a1edfae54fbd08483fcd90611050ab525b1db9e049bcb73eadbb8b02865c06d50d08f2e44b0b7e80bbd47'
            '989c432f8df1d040d253b735ba023a237b4594bb96b7b902f83d9648f8e82bf9e4d1f13a29c6c07c64d850674cc94c18cad295aae1ab2c6a5cea32637d51b085'
            'c242968c064ea957eaaac85d5d167041dfac4730949c51ff411c1ea81b75b795978b2c0134c856a48ad4c2318fd023b066ec57f087fb062cb6012403ebdf53a1')

prepare() {
  git -C ${srcdir}/qubes-core-admin am ${srcdir}/*.patch
}

build() {
  make -C ${srcdir}/qubes-core-admin BACKEND_VMM=xen all
}

check() {
  cd qubes-core-admin

  : "${PYTHON:=python3}"
  : "${TESTPYTHONPATH:=test-packages}"

  PYTHONPATH="${TESTPYTHONPATH}:${PYTHONPATH}"
  export PYTHONPATH

  "${PYTHON}" setup.py egg_info --egg-base "${TESTPYTHONPATH}"
  "${PYTHON}" -m coverage run --rcfile=ci/coveragerc -m qubes.tests.run "$@"
}

package() {
  statedir=/run

  make -C ${srcdir}/qubes-core-admin BACKEND_VMM=xen DESTDIR=${pkgdir} STATEDIR=${statedir} install

  # Remove cruft directories that shouldn't have been created in the first place.
  # Maybe patch these and send upstream?
  rm -rf ${pkgdir}/run
  rm -rf ${pkgdir}/var/log
  rm -rf ${pkgdir}/usr/libexec

  # We're sending all log output to journald, remove the logrotate.
  rm -rf ${pkgdir}/etc/logrotate.d/

  # Remove Python test code cruft.
  # TODO: would be much better to tame setuptools to just not install these.
  rm -rf ${pkgdir}/usr/lib/**/site-packages/**/tests

  install -Dm644 ${srcdir}/qubes.sysusers ${pkgdir}/usr/lib/sysusers.d/qubes.conf
}
