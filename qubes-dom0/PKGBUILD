# Maintainer: Sam Day <me@samcday.com>
pkgname=qubes-dom0
pkgver=mm_67faa7c1
pkgrel=1
pkgdesc="Qubes component: core-admin"
arch=(x86_64)
url="https://github.com/qubesos/qubes-core-admin"
license=('GPL')
depends=(
  python
  python-docutils
  python-jinja
  python-lxml
  python-setuptools
  python-pyaml
  xen-qubes
  libvirt-python
  pciutils
  qubes-db
  )
makedepends=(
  imagemagick
  lvm2
  python-dbus
  python-sphinx
  )
source=(
  "git+https://github.com/qubesos/qubes-core-admin?tag=${pkgver}"
  0001-template-tmpfiles-qubes.conf-to-make-var-run-dir-ove.patch
  qubes.sysusers
  )
sha512sums=('SKIP'
            '3ae8a7b54c44360a20ccf1df81ab108a886a73caf468402faaf0e48eb21927292630a6a3b3c41fe9c60ea552b62a6e41698357c38a9e4a60585e215b9bd959c5'
            '7dcff76193b9b802e896e3ce2ae87eff0b6b78808eb730144625ed12d3a0e1ce29585a86a2a23bbceeff586dfaf621c9a9e127935eabcc9a7f022da125927065')


prepare() {
  cd qubes-core-admin
  git apply ../0001-template-tmpfiles-qubes.conf-to-make-var-run-dir-ove.patch
}

build() {
  make -C ${srcdir}/qubes-core-admin BACKEND_VMM=xen all
}

check() {
  cd qubes-core-admin

  : "${PYTHON:=python3}"
  : "${TESTPYTHONPATH:=test-packages}"

  PYTHONPATH="${TESTPYTHONPATH}:${PYTHONPATH}"
  export PYTHONPATH

  "${PYTHON}" setup.py egg_info --egg-base "${TESTPYTHONPATH}"
  "${PYTHON}" -m coverage run --rcfile=ci/coveragerc -m qubes.tests.run "$@"
}

package() {
  statedir=/run

  make -C ${srcdir}/qubes-core-admin BACKEND_VMM=xen DESTDIR=${pkgdir} STATEDIR=${statedir} install

  # Remove cruft directories that shouldn't have been created in the first place.
  # Maybe patch these and send upstream?
  rm -rf ${pkgdir}/run
  rm -rf ${pkgdir}/var/log
  rm -rf ${pkgdir}/usr/libexec

  # We're sending all log output to journald, remove the logrotate.
  rm -rf ${pkgdir}/etc/logrotate.d/

  # Remove Python test code cruft.
  # TODO: would be much better to tame setuptools to just not install these.
  rm -rf ${pkgdir}/usr/lib/**/site-packages/**/tests

  install -Dm644 ${srcdir}/qubes.sysusers ${pkgdir}/usr/lib/sysusers.d/qubes.conf
}
