# Maintainer: Sam Day <me@samcday.com>
pkgname=qubes-dom0
pkgver=mm_67faa7c1
pkgrel=1
pkgdesc="Qubes component: core-admin"
arch=(x86_64)
url="https://github.com/qubesos/qubes-core-admin"
license=('GPL')
depends=(
  python
  python-docutils
  python-jinja
  python-lxml
  python-setuptools
  python-systemd
  python-pyaml
  xen-qubes
  libvirt-python
  pciutils
  qubes-db
  )
makedepends=(
  imagemagick
  lvm2
  python-dbus
  python-sphinx
  )
source=(
  "git+https://github.com/qubesos/qubes-core-admin?tag=${pkgver}"
  qubes.sysusers
  0001-make-runtime-dir-in-tmpfiles-qubes.conf-configurable.patch
  0002-qmemman-systemd-improvements.patch
  0003-qubesd-systemd-improvements.patch
  0004-qubes-special-case-dom0-qubesdb-connection.patch
  0005-qubesd-systemd-socket-activation-support.patch
  )
sha512sums=('SKIP'
            '7dcff76193b9b802e896e3ce2ae87eff0b6b78808eb730144625ed12d3a0e1ce29585a86a2a23bbceeff586dfaf621c9a9e127935eabcc9a7f022da125927065'
            'f53440809f21a8c040509caf50046139e7ab01d51559b7dea1dddcf386b77af1444ab6b6480d3c11a3d95d6f62593779eefcef15bc35c951fad5c60d6e3151af'
            '11f6579906469aa8092810bcd19f24ce4852fb80477cc6f2b318abb7ceeee6149fd73600b54ceb26919b6c7a1f51f8fa4146ebc1e0d123b09bbcd7ebc7628c7d'
            'a386c80e54c7cab9ce6f997a8011bcc902e3c14828dc090f1eae99b6ab3e011d154a0cb7068d3060bb2e0ba1cc88e3fb939211fdc07a75d377ca9f8eb58fe2ab'
            '653dc7e02c7baa0cb95818bba87947eb20ffc04a640780aaacd846dfcba6f27066e767591ba727a01675db65e5662405f2062129cf32b2669a30764ae5d7919e'
            'f1f62b8348d245a27dd46f5b1b172bb39d73d619bd838b674f9727c02a7fbeed0bd85b431bc730a0c0a005757ac70a03130879d84118e136e9a4d2210caa7073')


prepare() {
  git -C ${srcdir}/qubes-core-admin am ${srcdir}/*.patch
}

build() {
  make -C ${srcdir}/qubes-core-admin BACKEND_VMM=xen all
}

check() {
  cd qubes-core-admin

  : "${PYTHON:=python3}"
  : "${TESTPYTHONPATH:=test-packages}"

  PYTHONPATH="${TESTPYTHONPATH}:${PYTHONPATH}"
  export PYTHONPATH

  "${PYTHON}" setup.py egg_info --egg-base "${TESTPYTHONPATH}"
  "${PYTHON}" -m coverage run --rcfile=ci/coveragerc -m qubes.tests.run "$@"
}

package() {
  statedir=/run

  make -C ${srcdir}/qubes-core-admin BACKEND_VMM=xen DESTDIR=${pkgdir} STATEDIR=${statedir} install

  # Remove cruft directories that shouldn't have been created in the first place.
  # Maybe patch these and send upstream?
  rm -rf ${pkgdir}/run
  rm -rf ${pkgdir}/var/log
  rm -rf ${pkgdir}/usr/libexec

  # We're sending all log output to journald, remove the logrotate.
  rm -rf ${pkgdir}/etc/logrotate.d/

  # Remove Python test code cruft.
  # TODO: would be much better to tame setuptools to just not install these.
  rm -rf ${pkgdir}/usr/lib/**/site-packages/**/tests

  install -Dm644 ${srcdir}/qubes.sysusers ${pkgdir}/usr/lib/sysusers.d/qubes.conf
}
